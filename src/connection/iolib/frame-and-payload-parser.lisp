(in-package :cl-bunny)

(defstruct (fap-parser (:constructor make-fap-parser%))
  (buffer)
  (buffer-index)
  (end-index)
  (frame)
  (parser)
  (payload-parser))

(defun make-fap-parser ()
  (let ((fap-parser (make-fap-parser%)))
    (setf (fap-parser-parser fap-parser)
          (amqp:make-frame-parser
           :on-frame-type (lambda (parser frame-type)
                            (declare (ignore parser))
                            (setf (fap-parser-frame fap-parser) (make-instance (amqp:frame-class-from-frame-type frame-type))))
           :on-frame-channel (lambda (parser frame-channel)
                               (declare (ignore parser))
                               (setf (amqp:frame-channel (fap-parser-frame fap-parser)) frame-channel))
           :on-frame-payload-size (lambda (parser payload-size)
                                    (declare (ignore parser))
                                    ;; validate frame size
                                    (unless (= +amqp-frame-heartbeat+ (amqp::frame-type (fap-parser-frame fap-parser)))
                                      (setf (amqp:frame-payload-size (fap-parser-frame fap-parser)) payload-size
                                            (fap-parser-payload-parser fap-parser) (amqp:make-frame-payload-parser (fap-parser-frame fap-parser)))))
           :on-frame-payload (lambda (parser data start end)
                               (declare (ignore parser))
                               (when (fap-parser-payload-parser fap-parser)
                                 (amqp:frame-payload-parser-consume (fap-parser-payload-parser fap-parser) data :start start :end end)))
           :on-frame-end (lambda (parser)
                           (declare (ignore parser))
                           (when (fap-parser-payload-parser fap-parser)
                             (amqp:frame-payload-parser-finish (fap-parser-payload-parser fap-parser)))
                           ;;(setf frame-ended t)
                           )))
    fap-parser))

(defun fap-parser-advance (fap-parser read)
  (incf (fap-parser-end-index fap-parser) read)
  (multiple-value-bind (read-buffer-index parsed)
      (frame-parser-consume (fap-parser-parser fap-parser)
                            (fap-parser-buffer fap-parser)
                            :start (fap-parser-buffer-index fap-parser)
                            :end (fap-parser-end-index fap-parser))
    (incf (fap-parser-buffer-index fap-parser) read-buffer-index)
    (if parsed
        (fap-parser-frame fap-parser))))
